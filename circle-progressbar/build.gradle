apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

group = "io.ona.circle-progressbar"
version = '0.1.0'
def home = System.properties['user.home']

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

artifacts {
    archives sourceJar
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        CircleProgressbarLib(MavenPublication) {
            artifact("$buildDir/outputs/aar/circle-progressbar-release.aar")
            artifact(sourceJar)
            groupId this.group
            artifactId 'lib'
            version this.version

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            name = 'local'
            url = "file://${home}/.m2/repository/"
        }
    }
}

bintray {
    // Get Bintray credentials from environment variables
    user = System.getenv('BINTRAY_USER') // Get bintray User
    key = System.getenv('BINTRAY_KEY') // Get bintray Secret Key
    configurations = ['archives']
    pkg {
        repo = 'CircleProgressbar'
        name = project.name
        userOrg = 'ona'
        licenses = ['Apache-2.0']
        desc = 'A simple library for creating circular progressbars for Android.'
        websiteUrl = "https://github.com/onaio/CircleProgressbar"
        publish = true
        vcsUrl = 'https://github.com/onaio/CircleProgressbar.git'
        version {
            name = this.version
            desc = " ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
    publications = ['CircleProgressbarLib']
}